(define (domain sokoban-sequential)
  (:requirements :typing :action-costs)
  (:types thing location direction - object
          player stone - thing)
  (:predicates (clear ?l - location)
	       (at-player ?t - player ?l - location)
	       (at-stone ?t - stone ?l - location)
	       (at-goal ?s - stone)
	       (IS-GOAL ?l - location)
	       (IS-NONGOAL ?l - location)
               (MOVE-DIR ?from ?to - location ?dir - direction))
  
  (:functions (total-cost) )

  (:action move
   :parameters (?p - player ?from - location ?to - location ?dir - direction)
   :precondition (and (at-player ?p ?from)
                      (clear ?to)
                      (MOVE-DIR ?from ?to ?dir)
                      )
   :effect       (and (not (at-player ?p ?from))
                      (not (clear ?to))
                      (at-player ?p ?to)
                      (clear ?from)
                      )
   )

  (:action push-to-nongoal
   :parameters (?p - player ?s - stone
                ?ppos - location ?from - location ?to - location
                ?dir - direction)
   :precondition (and (at-player ?p ?ppos)
                      (at-stone ?s ?from)
                      (clear ?to)
                      (MOVE-DIR ?ppos ?from ?dir)
                      (MOVE-DIR ?from ?to ?dir)
                      (IS-NONGOAL ?to)
                      )
   :effect       (and (not (at-player ?p ?ppos))
                      (not (at-stone ?s ?from))
                      (not (clear ?to))
                      (at-player ?p ?from)
                      (at-stone ?s ?to)
                      (clear ?ppos)
                      (not (at-goal ?s))
                      (increase (total-cost) 1)
                      )
   )

  (:action push-to-goal
   :parameters (?p - player ?s - stone
                ?ppos - location ?from - location ?to - location
                ?dir - direction)
   :precondition (and (at-player ?p ?ppos)
                      (at-stone ?s ?from)
                      (clear ?to)
                      (MOVE-DIR ?ppos ?from ?dir)
                      (MOVE-DIR ?from ?to ?dir)
                      (IS-GOAL ?to)
                      )
   :effect       (and (not (at-player ?p ?ppos))
                      (not (at-stone ?s ?from))
                      (not (clear ?to))
                      (at-player ?p ?from)
                      (at-stone ?s ?to)
                      (clear ?ppos)
                      (at-goal ?s)
                      (increase (total-cost) 1)
                      )
   )
)
